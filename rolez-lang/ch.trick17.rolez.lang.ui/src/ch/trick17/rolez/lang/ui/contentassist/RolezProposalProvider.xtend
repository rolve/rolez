/*
 * generated by Xtext
 */
package ch.trick17.rolez.lang.ui.contentassist

import ch.trick17.rolez.lang.RolezExtensions
import ch.trick17.rolez.lang.rolez.Expr
import ch.trick17.rolez.lang.rolez.Field
import ch.trick17.rolez.lang.rolez.Member
import ch.trick17.rolez.lang.rolez.MemberAccess
import ch.trick17.rolez.lang.rolez.Method
import ch.trick17.rolez.lang.rolez.RoleType
import ch.trick17.rolez.lang.typesystem.RolezSystem
import ch.trick17.rolez.lang.typesystem.RolezUtils
import javax.inject.Inject
import org.eclipse.emf.ecore.EObject
import org.eclipse.xtext.Assignment
import org.eclipse.xtext.scoping.Scopes
import org.eclipse.xtext.ui.editor.contentassist.ContentAssistContext
import org.eclipse.xtext.ui.editor.contentassist.ICompletionProposalAcceptor

class RolezProposalProvider extends AbstractRolezProposalProvider {

    @Inject RolezSystem system
    @Inject RolezUtils utils
    @Inject extension RolezExtensions

    override completeFieldSelector_Field(EObject model, Assignment a, ContentAssistContext context,
        ICompletionProposalAcceptor acceptor) {
        completeMemberAccess(model, context, acceptor, Field)
    }

    override completeMethodSelector_Method(EObject model, Assignment a, ContentAssistContext context,
        ICompletionProposalAcceptor acceptor) {
        completeMemberAccess(model, context, acceptor, Method)
    }

    private def completeMemberAccess(EObject model, ContentAssistContext context, ICompletionProposalAcceptor acceptor,
        Class<? extends Member> kind) {
        var Expr target

        // Find target. Why the heck is this so complicated???
        if (model instanceof Expr) {
            target = model;
            if (model instanceof MemberAccess) {
                target = model.target
                if (model.eContainer instanceof MemberAccess)
                    target = (model.eContainer as MemberAccess).target as MemberAccess
            }
        }
        
        if (target != null) {
            val targetType = system.type(utils.envFor(model), target).value
            if (targetType instanceof RoleType) {
                val factory = getProposalFactory("MemberAccess", context)
                val scope = Scopes.scopeFor(targetType.base.clazz.allMembers.filter(kind))
                for (e : scope.allElements) {
                    if (!acceptor.canAcceptMoreProposals)
                        return;
                    acceptor.accept(factory.apply(e))
                }
            }
        }
    }
}
