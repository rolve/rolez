module ch.trick17.rolez.GenerateRolez

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe2.ecore.EcoreGenerator

var projectName = "ch.trick17.rolez"
var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					src = "" // Prevents the generation of the RolezParsingTest in the "wrong" pkg
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
			}
		}
		language = StandardLanguage {
			name = "${projectName}.Rolez"
			fileExtensions = "rz,rolez"
			referencedResource = "platform:/resource/${projectName}/model/Rolez.xcore"
			serializer = {
				generateStub = false
			}
			projectWizard = {
				generate = true
			}
			fragment = types.TypesGeneratorFragment2 {}
		}
	}
}
